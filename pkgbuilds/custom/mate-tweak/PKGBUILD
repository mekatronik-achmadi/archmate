pkgname=mate-tweak
pkgver=21.10.0
_umsver=21.10.0
pkgrel=1
pkgdesc="Tweak tool for MATE (fork of MintDesktop)"
arch=('any')
url="https://github.com/ubuntu-mate/mate-tweak"
license=('GPL')
depends=('gtk3' 'libnotify' 'mate-applets' 'python-configobj' 'python-distro' 'python-gobject' 'python-psutil' 'python-setproctitle')
makedepends=('python-distutils-extra' 'python-setuptools')
optdepends=('brisk-menu: for Manjaro panel layout'
            'mate-menu: to enable advanced menu')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ubuntu-mate/$pkgname/archive/$pkgver.tar.gz"
        "ubuntu-mate-settings-$_umsver.tar.gz::https://github.com/ubuntu-mate/ubuntu-mate-settings/archive/$_umsver.tar.gz")
sha256sums=('1bacc9b92960c712697e0c44fbcaaebed9683b4ff899af5af88506f7e13e1aff'
            '26856a73cf75c49e74cf77f6179ce69c7b6238dd2606d3cfdd2949983419fdc9')

prepare() {
  cd "$pkgname-$pkgver"

  #sed -i 's|/usr/lib/mate-applets/topmenu-mate-panel-applet|/usr/lib/topmenu-gtk/topmenu-mate-panel-applet|' mate-tweak
  sed -i 's|/usr/lib/linuxmint/mintMenu/mintMenu.py|/usr/lib/mate-menu/mate-menu.py|' mate-tweak

  # Disable Libnotify version check
  sed -i "s|gi.require_version('Notify', '0.7')|#gi.require_version('Notify', '0.7')|" mate-tweak
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
  cp -r "$srcdir"/ubuntu-mate-settings-$_umsver/usr/share/mate-panel "$pkgdir/usr/share"
}

